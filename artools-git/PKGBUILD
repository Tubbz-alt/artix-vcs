# Maintainer: artoo <artoo@manjaro.org>

_pkgbase=artools
_branch=devel

pkgbase=artools-git
pkgname=('artools-base-git'
        'artools-pkg-git'
        'artools-iso-base-git'
        'artools-iso-git')
pkgver=0.7.6.1.r15.g3e4fbd8
pkgrel=2
arch=('any')
pkgdesc='Development tools for Artix'
license=('GPL')
groups=('artools-git')
url='https://github.com/artix-linux/artools'
makedepends=('git')
source=("git+$url.git#branch=$_branch")
sha256sums=('SKIP')

prepare() {
    cd ${srcdir}/${_pkgbase}
    # patches here
    sed -e "s/^Version=.*/Version=$pkgver/" -i Makefile
}

pkgver() {
    cd ${srcdir}/${_pkgbase}
    git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cd ${srcdir}/${_pkgbase}
    make PREFIX=/usr
}

package_artools-base-git() {
    pkgdesc='Development tools for Artix (base tools)'
    provides=("artools-base=$_ver")
    depends=('openssh' 'rsync' 'haveged' 'os-prober' 'gnupg' 'pacman')
    optdepends=('artools-pkg: Artix package tools'
                'artools-iso: Artix iso tools'
                'artools-yaml: Artix yaml tools')
    conflicts=('artools-base')
    backup=('etc/artools/artools.conf')

    cd ${srcdir}/${_pkgbase}
    make PREFIX=/usr DESTDIR=${pkgdir} install_base
}

package_artools-pkg-git() {
    pkgdesc='Development tools for Artix (packaging tools)'
    provides=("artools-pkg=$_ver")
    depends=('namcap' 'artools-base-git' 'git')
    conflicts=('artools-pkg' 'devtools')

    cd ${srcdir}/${_pkgbase}
    make PREFIX=/usr DESTDIR=${pkgdir} install_pkg
}

package_artools-iso-git() {
    pkgdesc='Development tools for Artix (ISO tools)'
    provides=("artools-iso=$_ver")
    depends=('dosfstools' 'libisoburn' 'squashfs-tools'
            'mkinitcpio' 'mktorrent' 'grub' 'artools-base-git' 'artools-iso-base')
    conflicts=('artools-iso' 'artools-yaml')
    replaces=('artools-yaml')

    cd ${srcdir}/${_pkgbase}
    make PREFIX=/usr DESTDIR=${pkgdir} install_iso
}

package_artools-iso-base-git() {
    pkgdesc='Development tools for Artix (ISO base profile)'
    provides=('artools-iso-base')
    conflicts=('artools-iso-base')

    cd ${srcdir}/${_pkgbase}
    make PREFIX=/usr DESTDIR=${pkgdir} install_base_profile
}
