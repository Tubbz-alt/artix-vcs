# Maintainer: artoo <artoo@manjaro.org>

_pkgbase=artools
_ver=0.1
_branch=devel

pkgbase=artools-git
pkgname=('artools-base-git'
        'artools-pkg-git'
        'artools-iso-git'
        'artools-yaml-git')
pkgver=r47.8365754
pkgrel=1
arch=('any')
pkgdesc='Development tools for Artix'
license=('GPL')
groups=('artools-git')
url='https://github.com/artix-linux/artools'
makedepends=('git' 'docbook2x')
source=("git+$url.git#branch=$_branch")
sha256sums=('SKIP')

prepare() {
    cd ${srcdir}/${_pkgbase}
    # patches here
    sed -e "s/^Version=.*/Version=$_ver/" -i Makefile
    #sed -e "s/^Version=.*/Version=$pkgver/" -i Makefile
}

pkgver() {
    cd ${srcdir}/${_pkgbase}
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd ${srcdir}/${_pkgbase}
    make SYSCONFDIR=/etc PREFIX=/usr
}

package_artools-base-git() {
    pkgdesc='Development tools for Artix (base tools)'
    provides=("artools-base=$_ver")
    depends=('openssh' 'rsync' 'haveged' 'os-prober' 'gnupg' 'pacman')
    optdepends=('artools-pkg: Artix package tools'
                'artools-iso: Artix iso tools'
                'artools-yaml: Artix yaml tools')
    conflicts=('artools-base')
    backup=('etc/artools/artools.conf')

    cd ${srcdir}/${_pkgbase}
    make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_base
}

package_artools-pkg-git() {
    pkgdesc='Development tools for Artix (packaging tools)'
    provides=("artools-pkg=$_ver")
    depends=('namcap' 'artools-base-git')
    conflicts=('artools-pkg' 'devtools')

    cd ${srcdir}/${_pkgbase}
    make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_pkg
}

package_artools-yaml-git() {
    pkgdesc='Development tools for Artix (yaml tools)'
    provides=("artools-yaml=$_ver")
    conflicts=('artools-yaml')
    depends=('artools-base-git' 'artools-base-iso')

    cd ${srcdir}/${_pkgbase}
    make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_yaml
}

package_artools-iso-git() {
    pkgdesc='Development tools for Artix (ISO tools)'
    provides=("artools-iso=$_ver")
    depends=('dosfstools' 'libisoburn' 'squashfs-tools' 'artools-yaml-git'
            'mkinitcpio' 'mktorrent' 'grub')
    conflicts=('artools-iso')

    cd ${srcdir}/${_pkgbase}
    make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_iso
}

