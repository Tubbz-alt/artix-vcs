# Maintainer: Francois Menning <f.menning@pm.me>
# Contributor: Anton Kudryavtsev <anton@anibit.ru>
# Contributor: Frederik Schwan <frederik dot schwan at linux dot com>
# Contributor: Thomas Fanninger <thomas@fanninger.at>
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Thomas Laroche <tho.laroche@gmail.com>

_pkgname='gitea'
_gourl='code.gitea.io'
_gentoo="https://gitweb.gentoo.org/repo/gentoo.git/plain/www-apps/gitea/files"
pkgname=gitea-git
pkgrel=1
pkgver=r6540.ae9dd239f
pkgdesc='A painless self-hosted Git service.'
url='https://gitea.io/'
license=('MIT')
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h' 'aarch64')
depends=('git' 'go')
optdepends=('sqlite: SQLite support'
            'mariadb: MariaDB support'
            'postgresql: PostgreSQL support'
            'pam: Authentication via PAM support'
            'redis: Redis support'
            'memcached: MemCached support'
            'openssh: GIT over SSH support')
conflicts=('gitea')
provides=('gitea')
options=('!strip' 'emptydirs')
backup=('etc/gitea/app.ini')
install=gitea.install
source=('git://github.com/go-gitea/gitea.git'
	"${_gentoo}/gitea.confd"
	"${_gentoo}/gitea.initd-r1"
	"${_gentoo}/gitea.logrotated"
        '01-adjust-config.patch'
        'gitea.tmpfiles'
)
sha512sums=('SKIP'
            '3e05bad19cd924c4375d9ca6fc57142ba4b5ec897a1c789c3fc9710dfcfee209a199c95040eece089ad93e0357486ab98fad0c448a61e318646b7a71845e9a25'
            '4a6bba463c9abafcb6fc9f3854efd1a69fb3b213c2b34ff6a233f90db535c2ab0f375a0809e4c4be6651ed60d7696a54148a5ee9192567d70ca6a5203a8d3266'
            '798f3fc33026f19dd31ffa78b9b1ed313d7b82b19c047f509e896c1798b2925a1bb7c4998d98a6b47e728979bf7132fb1c2a80eb7fda096a763d6791631cb6d4'
            '3f96361a5135ea11b438e2cad29f2033221c63c11d1f260474d589c469e5db760fbf4da0718f9d015e106b72a13c02ad2899a8a90ac07365e20b935b59e95a6c'
            '0c6c9729f8dfd5b5fe2badf998e89624b00800f87ae1b28a68acd52f2621f3434cc3930a578d2bb3e27005f8ffbb0f4a0e4e4d3d2e2371d0214d36c805d65573')

pkgver() {
  cd "${srcdir}/${_pkgname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  sed -i -e "s/\"main.Version.*$/\"main.Version=$(pkgver)\"/" "${srcdir}/${_pkgname}/Makefile"
  patch -Np1 -i "${srcdir}/01-adjust-config.patch" "${srcdir}/${_pkgname}/custom/conf/app.ini.sample"

# Artix patches
  sed -e 's|/sbin/openrc|/usr/bin/openrc|' \
      -e "s/need net/need net mysql/" \
      -e "s/:-git/:-gitea/g" \
      -i ${srcdir}/${_pkgname}.initd-r1
  sed -e 's|/var/lib/gitea/conf/|/etc/gitea/|' -i  ${srcdir}/${_pkgname}.confd

  mkdir -p "${srcdir}/src/${_gourl}/${_pkgname}"
  cp -r "${srcdir}/${_pkgname}" "${srcdir}/src/${_gourl}"
}

build() {
  cd "${srcdir}/src/${_gourl}/${_pkgname}"
  PATH="${srcdir}/bin:$PATH" GOPATH="${srcdir}" make DESTDIR="${pkgdir}/" TAGS="sqlite pam" clean generate build
}

package() {
  cd "${srcdir}/src/${_gourl}/${_pkgname}"

  install -dm0750 "${pkgdir}/"{etc,var/log,var/lib}/${_pkgname}

  cp -r {custom,public,options,templates} "${pkgdir}/var/lib/${_pkgname}"

  install -Dm0755 "${_pkgname}" "${pkgdir}/usr/bin/${_pkgname}"
  install -Dm0644 "custom/conf/app.ini.sample" "${pkgdir}/etc/${_pkgname}/app.ini"
  install -Dm0644 "${srcdir}/${_pkgname}.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/${_pkgname}.conf"
  install -Dm0644 "LICENSE" "${pkgdir}/usr/share/licenses/${_pkgname}"
  install -Dm0644 "${srcdir}/${_pkgname}.confd" "${pkgdir}/etc/conf.d/${_pkgname}"
  install -Dm0644 "${srcdir}/${_pkgname}.logrotated" "${pkgdir}/etc/logrotate.d/${_pkgname}"
  install -Dm0755 "${srcdir}/${_pkgname}.initd-r1" "${pkgdir}/etc/init.d/${_pkgname}"
}
